plugins {
    id("java-library")
    id("org.spongepowered.plugin") version "0.9.0"
    id("com.github.johnrengelman.shadow") version "4.0.4"
}

group = "dev.flashlabs.cratecrate"
version = "0.0.2"

ext.spongeapiVersion = "7.4.0"
ext.flashlibsVersion = "0.1.2"
ext.stormVersion = "0.1.0"

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation("org.spongepowered:spongeapi:${spongeapiVersion}")
    implementation("com.github.Flash-Labs:FlashLibs:v${flashlibsVersion}")
    implementation("com.github.WillBAnders:Storm:v${stormVersion}")
}

//Sets plugin metadata through Gradle build instead of the @Plugin annotation.
sponge {
    plugin {
        meta {
            name = "CrateCrate"
            description = "The cratest crate plugin of all time"
            url = "https://github.com/flash-labs/CrateCrate"
            dependencies {
                flashlibs {
                    version = flashlibsVersion
                }
            }
        }
    }
}

shadowJar {
    dependencies {
        include dependency("com.github.WillBAnders:Storm:v${stormVersion}")
    }
    relocate("dev.willbanders.storm", "${project.group}.storm")
    version = "v$version"
    classifier = ""
}

//Produces a sources jar for providing source to consumers.
task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    version = "v$version"
    classifier = "sources"
}

//Produces a javadoc jar for rendering javadoc documentation.
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    version = "v$version"
    classifier = "javadoc"
}

//Includes custom jars in published artifacts for external access.
artifacts.archives (sourcesJar, javadocJar)

//Copies produced jar files into a project folder for releases by version.
task copyJars(type: Copy) {
    from (shadowJar, sourcesJar, javadocJar) into project.file("releases/${version}")
}
build.dependsOn(copyJars)
